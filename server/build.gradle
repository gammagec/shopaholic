plugins {
    id 'java'
    id 'com.google.protobuf' version "${protobufPluginVersion}"
    id 'application'
}

group = 'com.christophergammage'
version = '1.0.0'
mainClassName = 'com.christophergammage.shopaholic.ShopaholicServer'

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.4'
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
}

def libsDir = layout.buildDirectory.dir('libs')

tasks.register('copyDependencies', Copy) {
    into libsDir
    from configurations.runtimeClasspath
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        proto {
            srcDir "$rootDir/proto"
        }
    }
}

jar {
    manifest {
        attributes(
                "Class-Path": configurations.runtimeClasspath.files.collect {
                    it.getName()
                }.join(' '),
                "Manifest-Version": "1.0",
                "Main-Class": "$mainClassName"
        )
    }
}

application {
    mainClass ="$mainClassName"
}